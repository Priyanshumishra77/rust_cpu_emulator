use std::str::FromStr;
use crate::loader::ast::{ASTOperand, ASTInstr, ASTData, ASTSection, ASTAssembly, ASTDirective, ASTTextLine, ASTDataLine, ASTLabel,ASTPreamble};
use crate::cpu::{SP,FP,LR,PC};
// https://gist.github.com/brendanzab/4c5e5e1836ecc3a46afd05ed046c695c

grammar;

Integer: u64 = {
    r"[0-9]+" => u64::from_str(<>).unwrap()
};

Mnemonic: String = {
    r"[a-zA-Z]+" => String::from(<>),
};

VariableName: String = {
    r"[a-zA-Z]+" => String::from(<>),
};

Operand_Sep: () = {
    ","  => (),
}

LabelName: String = {
    r"[a-zA-Z]+" => String::from(<>),
}

DirectiveName: String = {
    r"\.[a-zA-Z]+" => String::from(<>),
}

Operand: ASTOperand = {
    Register,
    Immediate,
    LabelOperand,
    //MemoryAccess,
}
//
//MemoryAccess: Operand = {
//     "[" <r:Register> "]" => Operand::MemoryAccess(r),
//}

Register: ASTOperand = {
    <start:@L> <r:r"r[0-9]+">   =>
                                {
                                    let mut s = String::from(r);
                                    s.remove(0);
                                    ASTOperand::Register(s.parse::<u64>().unwrap(), start)
                                },
    <start:@L>  "fp"            => ASTOperand::Register(FP as u64, start),
    <start:@L>  "sp"            => ASTOperand::Register(SP as u64, start),
    <start:@L>  "lr"            => ASTOperand::Register(LR as u64, start),
    <start:@L>  "pc"            => ASTOperand::Register(PC as u64, start)
};

Immediate: ASTOperand = {
   // # todol space allowed and not good
    //"#" <Integer> => Operand::Immediate(<>),
    <start:@L> "#" <i:Integer> => ASTOperand::Immediate(i, start),
};

LabelOperand: ASTOperand = {
     <start:@L> <l:LabelName> => ASTOperand::Label(l, start),
};

Directive: ASTDirective = {
   <start:@L> ".global" <l:LabelName> => ASTDirective::Global(l, start),
}

DataLine: ASTDataLine = {
    Data => ASTDataLine::Data(<>),
    Directive => ASTDataLine::Directive(<>)
}

Section:ASTSection = {
    DataSection,
    TextSection
}

Data: ASTData = {
    <start:@L> <n:VariableName> ":" ".word" <v:Integer> => ASTData{name:n, value:v, pos:start}
}

DataSection:ASTSection = {
    ".data" <DataLine+>                 => ASTSection::Data(<>),
    ".section" ".data" <DataLine+>      => ASTSection::Data(<>),
}

Label: ASTLabel = {
    <start:@L> <n:LabelName> ":" => ASTLabel{name:n, pos:start},
}

Instr: ASTInstr = {
//   <start:@L> <m:Mnemonic>
//            => Instr{mnemonic:m, op1:Operand::Unused(), op2:Operand::Unused(), op3:Operand::Unused()},
   <start:@L> <m:Mnemonic>  <o1:Operand>
             => ASTInstr{mnemonic:m, op1:o1, op2:ASTOperand::Unused(), op3:ASTOperand::Unused(), pos:start},
   <start:@L> <m:Mnemonic>  <o1:Operand> Operand_Sep <o2:Operand>
             => ASTInstr{mnemonic:m, op1:o1, op2:o2, op3:ASTOperand::Unused(), pos:start},
   <start:@L> <m:Mnemonic>  <o1:Operand> Operand_Sep <o2:Operand> Operand_Sep <o3:Operand>
             => ASTInstr{mnemonic:m, op1:o1, op2:o2, op3:o3, pos:start},
}

TextSection: ASTSection = {
    ".text" <TextLine+>             => ASTSection::Text(<>),
    ".section" ".text" <TextLine+>  => ASTSection::Text(<>),
}

TextLine: ASTTextLine = {
    Instr                           => ASTTextLine::Text(<>),
    Directive                       => ASTTextLine::Directive(<>),
    Label                           => ASTTextLine::Label(<>),
}

Preamble: ASTPreamble ={
    Directive* => ASTPreamble{directives:<>},
}

pub Assembly:ASTAssembly = {
    <p:Preamble> <s:Section*> => ASTAssembly{preamble:p, sections:s},
}