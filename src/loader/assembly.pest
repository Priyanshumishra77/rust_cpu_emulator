whitespace = _{ (" " | "\t")+ }

new_line = _{ "\n" }

comma = _{ "," }

arg_sep = _{ whitespace? ~ comma ~ whitespace?}

empty_line = _{ whitespace? ~ comment? ~ new_line }

alpha = { 'a'..'z' | 'A'..'Z' }

digit = { '0'..'9' }

comment = _{ ";" ~ (!"\n" ~ ANY)* }

memory_access = {"[" ~ ( variable_name | register )  ~ "]"}

register = { ( "R" | "r" ) ~ integer}

label_name = { (alpha | digit | "_" )+ }

variable_name = { alpha ~ (alpha | digit| "_" )* }

integer = { digit+ }

file = _{ SOI ~ assembly ~ EOI }

assembly = _{ empty_line* ~ data_section? ~ global_section }

data_section = _{ whitespace? ~ ".data" ~ whitespace? ~ comment? ~ data_line*}

data_line = _{ new_line ~ whitespace? ~ data ~ whitespace? ~ comment?}

data = { variable_name ~ whitespace ~ "DCD" ~ whitespace ~ integer}

global_section = _{ new_line ~  whitespace? ~ ".global" ~ whitespace? ~ comment? ~ (global_line | empty_line)* }

global_line = _{ new_line ~ whitespace? ~ ( instr | label ) ~ whitespace? ~ comment? }

label = { label_name ~ ":" }

instr = _{  ( instr_LDR
            | instr_STR
            | instr_MOV
            | instr_AND
            | instr_OR
            | instr_XOR
            | instr_DEC
            | instr_INC
            | instr_ADD
            | instr_SUB
            | instr_MUL
            | instr_DIV
            | instr_MOD
            | instr_PRINTR
            | instr_NOP
            | instr_EXIT
            | instr_JNZ
            | instr_JZ
            | instr_PUSH
            | instr_POP
            | instr_NOT
            | instr_CALL
            | instr_RET
            )}

// the order for all arguments is source and then sink. E.g. 'ADD R1 R2 R3' means R3=R1+R2'.
// math operations
instr_INC       = {"INC"    ~ whitespace ~ register                                                     }
instr_DEC       = {"DEC"    ~ whitespace ~ register                                                     }
instr_ADD       = {"ADD"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_SUB       = {"SUB"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_NEG       = {"NEG"    ~ whitespace ~ register                                                     }
instr_DIV       = {"DIV"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_MUL       = {"MUL"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_MOD       = {"MOD"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
// bitwise logical instructions
instr_AND       = {"AND"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_OR        = {"OR"     ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_XOR       = {"XOR"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
instr_NOT       = {"NOT"    ~ whitespace ~ register ~ arg_sep ~ register ~ arg_sep ~ register     }
// conditional jumps
instr_JNZ       = {"JNZ"    ~ whitespace ~ register ~ arg_sep ~ label_name                           }
instr_JZ        = {"JZ"     ~ whitespace ~ register ~ arg_sep ~ label_name                           }
// stack operations
instr_PUSH      = {"PUSH"   ~ whitespace ~ register                                                     }
instr_POP       = {"POP"    ~ whitespace ~ register                                                     }
// various
instr_PRINTR    = {"PRINTR" ~ whitespace ~ register                                                     }
instr_NOP       = {"NOP"                                                                                }
instr_EXIT      = {"EXIT"                                                                               }
// memory & register copying
instr_LDR       = {"LDR"   ~ whitespace ~ register  ~ arg_sep ~ memory_access                        }
instr_STR       = {"STR"   ~ whitespace ~ register ~ arg_sep ~ memory_access                         }
instr_MOV       = {"MOV"    ~ whitespace ~ register ~ arg_sep ~ register                             }
// procedure call
instr_CALL      = {"CALL"  ~ whitespace ~ label_name                                                    }
instr_RET       = {"RET"                                                                                }